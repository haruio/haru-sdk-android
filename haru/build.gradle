buildscript {
    repositories {
        mavenCentral()
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

version = '0.1.0'
group = 'io.plugy'

android {
    compileSdkVersion 18
    buildToolsVersion "20.0.0"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 18
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            java {
                srcDir 'src/main/java'
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':facebook')
    compile project(':kakao')
}

android.libraryVariants.all { variant ->
    task("generate${variant.name}Javadoc", type: Javadoc) {
        source = variant.javaCompile.source
        def androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"

        classpath = files(variant.javaCompile.classpath.files, androidJar)
        options {
            encoding "UTF-8"
            links "http://docs.oracle.com/javase/7/docs/api/"
            linksOffline "http://d.android.com/reference","${android.sdkDirectory}/docs/reference"
        }
        exclude '**/BuildConfig.java'
        title 'Plugy Android SDK'
    }
}

signing {
    required { has("release") && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

def ossrhUsername = hasProperty('OSSRH_USERNAME') ? OSSRH_USERNAME : ""
def ossrhPassword = hasProperty('OSSRH_PASSWORD') ? OSSRH_PASSWORD : ""

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            configuration = configurations.archives

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {

                name 'Plugy SDK for Android'
                artifactId = 'core'
                packaging 'aar'
                description "Plugy is mobile Backend-as-a-Service (BaaS) and this is SDK of Plugy."
                url 'https://github.com/soma5mbaas/haru-sdk-android.git'
                inceptionYear '2014'

                scm {
                    url 'https://github.com/soma5mbaas/haru-sdk-android.git'
                    connection 'scm:git:git://github.com/soma5mbaas/haru-sdk-android.git'
                    developerConnection 'scm:git:ssh://git@github.com/soma5mbaas/haru-sdk-android.git'
                    tag 'HEAD'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
                organization {
                    name 'Plugy'
                    url 'http://plugy.io'
                }
                developers {
                    developer {
                        id 'retail3210'
                        name 'Hyojun Kim'
                        email 'retail3210@gmail.com'
                    }
                }
            }
        }
    }
}